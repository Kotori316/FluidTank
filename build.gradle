import java.nio.file.Files
import java.nio.file.Paths
import java.util.stream.Collectors

plugins {
    id "scala"
    id("maven-publish")
    // https://maven.fabricmc.net/net/fabricmc/fabric-loom/
    id 'fabric-loom' version "0.11-SNAPSHOT"
    // id 'maven-publish'
    //noinspection SpellCheckingInspection
    id "com.github.breadmoirai.github-release" version "2.2.12"
    //noinspection SpellCheckingInspection
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

if (isInCI()) {
    String postFix = System.getenv("GITHUB_RUN_NUMBER") ?: "SNAPSHOT"
    // In remote CI/CD environment
    version = (version + "." + postFix + "-fabric")

    System.out.println("Set version to $version because I'm in CI." as String)
    System.out.println("CURSEFORGE_KEY: ${System.getenv().containsKey('CURSEFORGE_KEY')}" as String)
    System.out.println("AZURE_TOKEN: ${System.getenv().containsKey('AZURE_TOKEN')}" as String)
} else {
    version = (version + ".0" + "-fabric")
}

loom {
    runs {
        client {
            name "Client"
            runDir = Boolean.parseBoolean(System.getenv("FABRIC_SERVER")) ? "run-server" : "run"
        }
        server {
            name "Server"
            runDir = "run-server"
        }
        data {
            client()
            name "Data Generation"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.datagen"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.datagen.strict-validation"

            ideConfigGenerated = true
            runDir "run-server"
        }
        gameTest {
            name "GameTest"
            server()
            vmArg "-ea"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.gametest"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/test-results/test/game_test.xml"
            runDir = "build/game_test"
            source sourceSets.test
        }
    }
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/generated/resources'
        }
    }
}

@SuppressWarnings("SpellCheckingInspection")
private static boolean isInCI() {
    return Boolean.parseBoolean(System.getenv("GITHUB_ACTIONS")) || Boolean.parseBoolean(System.getenv("CI")) ||
            (!System.getProperty("os.name").toLowerCase().startsWith("windows") && Files.exists(Paths.get("/", ".dockerenv")))
}

repositories {
    maven {
        name = "BuildCraft"
        url = "https://alexiil.uk/maven/"
        content {
            //noinspection SpellCheckingInspection
            it.includeGroup("alexiil.mc.lib")
            //noinspection SpellCheckingInspection
            it.includeGroup("alexiil.mc.mod")
        }
    }
    maven {
        name = "What The Hell Is That"
        url = "https://maven.bai.lol"
        content {
            it.includeVersion("mcp.mobius.waila", "wthit-api", "fabric-${project.wthit_version}")
            it.includeVersion("mcp.mobius.waila", "wthit", "fabric-${project.wthit_version}")
        }
    }
    maven {
        name = "TechReborn"
        url = "https://modmaven.dev/"
    }
    maven {
        name = "REI"
        url = "https://maven.shedaniel.me/"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    //noinspection SpellCheckingInspection
    modImplementation "net.fabricmc:fabric-language-scala:${project.fabric_scala_version}"

    // library
    implementation "org.scala-lang:scala-library:2.13.6"
    implementation "com.google.code.findbugs:jsr305:3.0.1"
    //noinspection SpellCheckingInspection
    modImplementation "alexiil.mc.lib:libblockattributes-all:${project.bc_attribute_version}"

    //noinspection SpellCheckingInspection
    modRuntimeOnly("alexiil.mc.mod:simplepipes-all:${project.simple_pipe_version}")
    //noinspection SpellCheckingInspection
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}")
    modImplementation("RebornCore:RebornCore-1.18:${project.RebornCore_version}") {
        //noinspection SpellCheckingInspection
        exclude(group: "net.fabricmc.fabric-api")
    }
    modRuntimeOnly("TechReborn:TechReborn-1.18:${project.TechReborn_version}") {
        //noinspection SpellCheckingInspection
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompileOnly("mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}")
    modRuntimeOnly("mcp.mobius.waila:wthit:fabric-${project.wthit_version}")

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        //noinspection SpellCheckingInspection
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("appeng:appliedenergistics2-fabric:${project.ae2_version}") {
        transitive(false)
    }
    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.

    // Test Dependencies.
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2')
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.2')
    testRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2')
    testImplementation(group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.6.2')
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
    toolchain {
        it.languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from "LICENSE"
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}
test.dependsOn runGameTest

def BRANCH = "1.18-fabric"

// PUBLISH
def joinedLog = String.join("\n", List.of(
        "Fluid Tank for ${project.minecraft_version} and Fabric.",
        "",
        isInCI() ? "Released via GitHub Action. This version may contain some bugs!" : "",
        "",
        "Requires:",
        "",
        "* [Fabric Language Scala](https://www.curseforge.com/minecraft/mc-mods/fabric-language-scala)",
        "* [LibBlockAttributes](https://www.curseforge.com/minecraft/mc-mods/libblockattributes)",
        "* [Cloth Config API (Fabric)](https://www.curseforge.com/minecraft/mc-mods/cloth-config)",
        "",
        "See https://github.com/Kotori316/FluidTank/tree/${BRANCH} to get all change log.",
        "",
        "Built with:",
        "",
        "* Minecraft ${project.minecraft_version}",
        "* Mapping Official ${project.minecraft_version}",
        "* Fabric API ${project.fabric_version}",
        "* Fabric Loader ${project.loader_version}",
).stream().map { it.toString() }.toList())

githubRelease {
    repo.set('FluidTank')
    token = project.hasProperty("githubToken") ? getProperty("githubToken") : ""
    targetCommitish.set(BRANCH)

    body.set(joinedLog)
    releaseAssets = fileTree("build/libs")
}

curseforge {
    apiKey = project.hasProperty("curseforge_additional-enchanted-miner_key") ? getProperty("curseforge_additional-enchanted-miner_key") : (System.getenv("CURSEFORGE_KEY") ?: "")
    project {
        id = '411564'
        changelogType = 'markdown'
        addGameVersion 'Fabric'
        addGameVersion '1.18.1'
        changelog = joinedLog
        releaseType = isInCI() ? 'alpha' : 'release'
        mainArtifact remapJar
        addArtifact jar
        relations {
            requiredDependency 'fabric-language-scala'
            //noinspection SpellCheckingInspection
            requiredDependency 'libblockattributes'
            requiredDependency 'cloth-config'
        }
    }
    options {
        curseGradleOptions.debug = false
        javaVersionAutoDetect = false
        forgeGradleIntegration = false
    }
}

task getCompileOptions() {
    doLast {
        List<String> strings = new ArrayList<>()
        strings.add("archivesBaseName: $archivesBaseName")
        strings.add("group: $group")
        strings.add("project.maven_group: ${project.maven_group}")
        strings.add("project.group: ${project.group}")
        strings.add("version: $version")
        strings.add("version in config: ${project.version}")
        strings.add("runs: ${loom.getRunConfigs().asMap.toMapString()}")
        strings.add("Absolute Client RunDir: ${project.projectDir.toPath().resolve(loom.getRunConfigs().getByName("client").runDir).toAbsolutePath()}")
        strings.add("Absolute Server RunDir: ${project.projectDir.toPath().resolve(loom.getRunConfigs().getByName("server").runDir).toAbsolutePath()}")
        strings.add("-" * 20)
        strings.add("Jar: ${jar.getArchiveFile().get()}")
        strings.add("Remap Jar ${remapJar.getArchiveFile().get()}")
        strings.add("Source Jar ${sourcesJar.getArchiveFile().get()}")
        // strings.add("RemapSource Jar ${remapSourcesJar.getOutput()}")
        strings.add(String.valueOf(fileTree("build/libs").getFiles()))

        println(strings.stream().collect(Collectors.joining(System.lineSeparator())))
    }
}

publishing {
    repositories {
        maven {
            name = "AzureRepository"
            url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
            credentials {
                username = project.findProperty("azureUserName") ?: System.getenv("AZURE_USER_NAME") ?: ""
                password = project.findProperty("azureToken") ?: System.getenv("AZURE_TOKEN") ?: "TOKEN"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = "FluidTank".toLowerCase()
            if (isInCI()) {
                version = "${project.snapshot_version}.0-fabric-SNAPSHOT"
            } else {
                version = "${project.mod_version}.0-fabric"

            }
            from components.java
            pom {
                name = archivesBaseName
                description = "FluidTank for Minecraft ${project.minecraft_version}, build with Fabric ${project.fabric_version}"
                url = 'https://github.com/Kotori316/FluidTank/commits/' + BRANCH
                packaging = "jar"
                withXml {
                    //noinspection SpellCheckingInspection
                    List<String> requiredArtifacts = ["cloth", "fabric-language-scala", "libblockattributes"]
                    def pomNode = asNode() as Node
                    Set<String> added = []
                    pomNode.dependencies.'*'.findAll() { Node node ->
                        def valueList = node.value() as NodeList
                        String artifactId = ((valueList.get(1) as Node).value() as NodeList).get(0)
                        if (!added.contains(artifactId)) {
                            if (requiredArtifacts.stream().noneMatch { artifactId.contains(it) }) {
                                node.parent().remove(node)
                            } else {
                                added.add(artifactId)
                            }
                        }
                    }
                }
            }
        }
    }
}
