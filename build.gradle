plugins {
    id "scala"
    id 'fabric-loom' version '0.5.22'
    id 'maven-publish'
    id "com.github.breadmoirai.github-release" version "2.2.12"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileScala.sourceCompatibility = compileScala.targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-scala:0.3.0.+"

    // library
    implementation "org.scala-lang:scala-library:2.13.0"
    implementation "com.google.code.findbugs:jsr305:3.0.1"
    //noinspection SpellCheckingInspection
    modCompile "alexiil.mc.lib:libblockattributes-all:${project.bc_attribute_version}"

    //noinspection SpellCheckingInspection
    modRuntime("alexiil.mc.mod:simplepipes-all:${project.simple_pipe_version}")
    //noinspection SpellCheckingInspection
    modRuntime("me.shedaniel:RoughlyEnoughItems:${project.rei_version}")

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.

    // Test Dependencies.
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2')
    testRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2')
    testImplementation(group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.6.2')
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(ScalaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

test {
    useJUnitPlatform()
}

// PUBLISH
def joinedLog = String.join("\n",
        "Fluid Tank for 1.16.2 and Fabric.",
        "",
        "Requires:",
        "* [Fabric Language Scala](https://www.curseforge.com/minecraft/mc-mods/fabric-language-scala)",
        "* [LibBlockAttributes](https://www.curseforge.com/minecraft/mc-mods/libblockattributes)",
        "",
        "See https://github.com/Kotori316/FluidTank/tree/1.16.2-fabric to get all change log."
)

githubRelease {
    repo.set('FluidTank')
    token = project.hasProperty("githubToken") ? getProperty("githubToken") : ""
    targetCommitish.set('1.16.2-fabric')

    body.set(joinedLog)
    releaseAssets = files(
            jar.getArchiveFile().get(),
            sourcesJar.getArchiveFile().get(),
            remapJar.getArchiveFile().get()
    )
}

curseforge {
    apiKey = project.hasProperty("curseforge_additional-enchanted-miner_key") ? getProperty("curseforge_additional-enchanted-miner_key") : ""
    project {
        id = '291006'
        changelogType = 'markdown'
        addGameVersion 'Fabric'
        addGameVersion '1.16.2'
        changelog = joinedLog
        releaseType = 'beta'
        mainArtifact remapJar
        addArtifact jar
        addArtifact sourcesJar
        relations {
            requiredDependency 'fabric-language-scala'
            //noinspection SpellCheckingInspection
            requiredDependency 'libblockattributes'
        }
    }
    options {
        curseGradleOptions.debug = false
        javaVersionAutoDetect = false
        forgeGradleIntegration = false
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
